*$
********************************************************************************************
* (C) Copyright 2021 Texas Instruments Incorporated. All rights reserved.
* TI Confidential - Selective Disclosure
********************************************************************************************
*SIM2PSPICE Library 
*Last update :Jan 7th 2022
*Contact : eric.lee@ti.com, david.baba@ti.com
********************************************************************************************
*$
.SUBCKT AMUX2_S2P H L Y SEL PARAMS: VTH=0.5
S1 H Y SEL 0 SM1
RD1 SEL 0 1T
.MODEL SM1 VSWITCH ROFF=1T RON=1N VH=1M VT={VTH}
S2 L Y 0 SEL SM2
RD2 0 SEL 1T
.MODEL SM2 VSWITCH ROFF=1T RON=1N VH=1M VT={-VTH}
.ENDS AMUX2_S2P
*$
.SUBCKT GMAMP2_S2P POS NEG EN HIZ OUT MinClamp MaxClamp PARAMS: GAIN=1m ISOURCE=100u ISINK=100u RO=10MEG CO=1P
G1 0 INT1 VALUE {LIMIT(V(POS,NEG)*{GAIN}, {-ISINK},{ISOURCE})}
R1 INT1 0  {RO} 
C1 INT1 0  {CO} 
S1 INT1 0 0 EN SM1
RD1 0 EN 1T
.MODEL SM1 VSWITCH ROFF=1T RON=100 VOFF=-0.55 VON=-0.45
D1 INT1 MAXCLAMP DM1
D2 MINCLAMP INT1 DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
S2 INT1 OUT 0 HIZ SM2
RD2 0 HIZ 1T
.MODEL SM2 VSWITCH ROFF=1T RON=1m VOFF=-0.55 VON=-0.45 
.ENDS GMAMP2_S2P
*$
.SUBCKT GMAMP4_S2P POSA POSB NEGA NEGB EN HIZ OUT MinClamp MaxClamp PARAMS: GAIN=1m ISOURCE=100u ISINK=100u RO=10MEG CO=1P
EP1 POS 0 VALUE {MIN(V(POSA,0),V(POSB,0))}
EN1 NEG 0 VALUE {MAX(V(NEGA,0),V(NEGB,0))}
G1 0 INT1 VALUE {LIMIT(V(POS,NEG)*{GAIN}, {-ISINK},{ISOURCE})}
R1 INT1 0  {RO} 
C1 INT1 0  {CO} 
S1 INT1 0 0 EN SM1
RD1 0 EN 1T
.MODEL SM1 VSWITCH ROFF=1T RON=100 VOFF=-0.55 VON=-0.45
D1 INT1 MAXCLAMP DM1
D2 MINCLAMP INT1 DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
S2 INT1 OUT 0 HIZ SM2
RD2 0 HIZ 1T
.MODEL SM2 VSWITCH ROFF=1T RON=1m VOFF=-0.55 VON=-0.45 
.ENDS GMAMP4_S2P
*$
.SUBCKT VMUX_S2P H L Y SEL PARAMS: VTH=0.5 RC=1n
E1 YINT 0 VALUE {IF(V(SEL)>{VTH},V(H),V(L))}
RINT YINT Y 1
CINT Y 0 {RC}
.ENDS VMUX_S2P
*$
.SUBCKT IMUX_S2P HA HK LA LK YA YK SEL PARAMS: VTH=0.5
V1 HA HK 0 
V2 LA LK 0
H1 INTH 0 V1 1
H2 INTL 0 V2 1
G1 YA YK VALUE {IF(V(SEL)>{VTH},V(INTH),V(INTL))} 
.ENDS IMUX_S2P 
*$
.SUBCKT ASUM2_S2P A B Y PARAMS: COEFFA=1 COEFFB=1
E1 Y 0 VALUE {V(A,0)*{COEFFA}+V(B,0)*{COEFFB}}
.ENDS ASUM2_S2P 
*$
.SUBCKT ASUM3_S2P A B C Y PARAMS: COEFFA=1 COEFFB=1 COEFFC=1
E1 Y 0 VALUE {V(A,0)*{COEFFA}+V(B,0)*{COEFFB}+V(C,0)*{COEFFC}}
.ENDS ASUM3_S2P 
*$
.SUBCKT ASUB2_S2P A B Y PARAMS: COEFFA=1 COEFFB=1
E1 Y 0 VALUE {V(A,0)*{COEFFA}-V(B,0)*{COEFFB}}
.ENDS ASUB2_S2P 
*$
.SUBCKT OPAMP2_S2P POS NEG EN HIZ OUT MaxClamp MinClamp PARAMS: GAIN=10k ISOURCE=5m ISINK=5m HEADROOM=0 BW=5MEG PSR=1MEG NSR=1MEG ROUT=0.1
G1 0 INT1 VALUE {LIMIT(((V(POS,NEG))*{GAIN/BW}),{-NSR*1E-9},{PSR*1E-9})}
R1 MinClamp INT1 {1/(2*3.1416*BW/GAIN*1E-9)} 
C1 INT1 0 1E-9
D1 INT1 MaxClamp1 DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
S1 INT1 0 0 EN SM1
RD1 0 EN 1T
.MODEL SM1 VSWITCH ROFF=1T RON=100 VOFF=-0.55 VON=-0.45   
V1 MaxClamp1 MaxClamp {HEADROOM}
D2 MinClamp1 INT1 DM1 
V2 MinClamp1 MinClamp {HEADROOM}
E1 INT2 MinClamp VALUE {V(INT1,MinClamp)}
I1 INT2 CLIM {ISOURCE}
D3 CLIM INT2 DM1     
I2 OUT1 CLIM {ISINK} 
D4 CLIM OUT1 DM1
S2 OUT1 OUT2 0 HIZ SM2
RD2 0 HIZ 1T
.MODEL SM2 VSWITCH ROFF=1T RON=1m VOFF=-0.55 VON=-0.45 
R2 OUT2 OUT {ROUT}
.ENDS OPAMP2_S2P
*$
.SUBCKT OPAMP4_S2P POSA POSB NEGA NEGB EN HIZ OUT MaxClamp MinClamp PARAMS: GAIN=10k ISOURCE=5m ISINK=5m HEADROOM=0 BW=5MEG PSR=1MEG NSR=1MEG ROUT=0.1
EP1 POS 0 VALUE {MIN(V(POSA,0),V(POSB,0))}
EN1 NEG 0 VALUE {MAX(V(NEGA,0),V(NEGB,0))}
G1 0 INT1 VALUE {LIMIT(((V(POS,NEG))*{GAIN/BW}),{-NSR*1E-9},{PSR*1E-9})}
R1 MinClamp INT1 {1/(2*3.1416*BW/GAIN*1E-9)} 
C1 INT1 0 1E-9
D1 INT1 MaxClamp1 DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
S1 INT1 0 0 EN SM1
RD1 0 EN 1T
.MODEL SM1 VSWITCH ROFF=1T RON=100 VOFF=-0.55 VON=-0.45   
V1 MaxClamp1 MaxClamp {HEADROOM}
D2 MinClamp1 INT1 DM1 
V2 MinClamp1 MinClamp {HEADROOM}
E1 INT2 MinClamp VALUE {V(INT1,MinClamp)}
I1 INT2 CLIM {ISOURCE}
D3 CLIM INT2 DM1     
I2 OUT1 CLIM {ISINK} 
D4 CLIM OUT1 DM1
S2 OUT1 OUT2 0 HIZ SM2
RD2 0 HIZ 1T
.MODEL SM2 VSWITCH ROFF=1T RON=1m VOFF=-0.55 VON=-0.45 
R2 OUT2 OUT {ROUT}
.ENDS OPAMP4_S2P
*$
.SUBCKT ILIM_S2P P N PARAMS: ISOURCE=10m ISINK=10m
I1 P CLIM {ISOURCE}
D3 CLIM P DM1     
I2 N CLIM {ISINK} 
D4 CLIM N DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
.ENDS ILIM_S2P
*$
.SUBCKT ASH_S2P IN CLK OUT PARAMS: VTH=0.5
E1 INT1 0 VALUE {V(IN,0)}
S1 INT1 INT2 CLK 0 SM1
RD1 CLK 0 1T
.MODEL SM1 VSWITCH ROFF=1T RON=1 VH=50m VT={VTH}
C1 INT2 0 1u
E2 OUT 0 VALUE {V(INT2,0)}
.ENDS ASH_S2P
*$
.SUBCKT ACOUNTER_S2P CLK CLR OUT PARAMS: MAX_VALUE=15 MAX_STEP=15 VTH=0.5
S1 NEW SAMPLE CLK 0 SM1
RD1 CLK 0 1T
.MODEL SM1 VSWITCH ROFF=1T RON=1m VH=100m VT={VTH}
S2 SAMPLE HOLD CLK 0 SM2
RD2 CLK 0 1T
.MODEL SM2 VSWITCH ROFF=1m RON=1T VH=100m VT={VTH}
CSAMPLE SAMPLE 0 1u IC=0 
CHOLD HOLD 0 1n IC=0 
ENEW OUT1 0 VALUE {V(HOLD,0)} 
VSTEP NEW OUT1 1

EOUT OUT 0 VALUE {V(OUT1,0)*MAX_VALUE/MAX_STEP}
ERST RST 0 VALUE {IF((V(SAMPLE,0)>{MAX_STEP+0.5/MAX_STEP}) | V(CLR,0)>{VTH},1,0)}
S3 HOLD 0 RST 0 SM3
RD3 RST 0 1T
.MODEL SM3 VSWITCH ROFF=1T RON=1u VH=50m VT=0.5
CDLY RST 0 10n IC=0 
.ENDS ACOUNTER_S2P
*$
.SUBCKT DRIVER_S2P INP INN VH VL OUTP OUTN PARAMS: RH=1 ISOURCE=3 RL=0.5 ISINK=5 VTH=0.5 VSATH=0.1 VSATL=0.1
SH VH OUTP1 GH INN SM1
RD1 GH INN 1T
.MODEL SM1 VSWITCH ROFF=1T RON={RH} VH=1m VT=1
GH OUTP1 OUTP VALUE {LIMIT((V(GH,INN)-1)*{ISOURCE},0,100000)}
DH OUTPS VH DSAT1
.MODEL DSAT1 D IS=1E-015 TT=1E-010 RS=1u N=0.001
ESATH OUTPS OUTP VALUE {VSATH}

SL OUTN VL1 GL INN SM2
RD2 GL INN 1T
.MODEL SM2 VSWITCH ROFF=1T RON={RL} VH=1m VT=1
GL VL1 VL VALUE {LIMIT((V(GL,INN)-1)*{ISINK},0,100000)}
DL VLS OUTN DSAT1
ESATL VLS VL VALUE {VSATL}

EGH GH1 INN VALUE {IF(V(INP,INN)>{VTH},2,0)}
RGH GH1 GH 1 
CGH GH INN 3n
EGL GL1 INN VALUE {IF(V(INP,INN)>{VTH-1m},0,2)}
RGL GL1 GL 1 
CGL GL INN 3n
.ENDS DRIVER_S2P
*$