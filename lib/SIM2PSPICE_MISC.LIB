*$
********************************************************************************************
* (C) Copyright 2021 Texas Instruments Incorporated. All rights reserved.
* TI Confidential - Selective Disclosure
********************************************************************************************
*SIM2PSPICE Library 
*Last update :Jan 7th 2022
*Contact : eric.lee@ti.com, david.baba@ti.com
********************************************************************************************
*$
.SUBCKT D_S2P P N  
D1 P N DM1
.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001
.ENDS D_S2P
*$
.SUBCKT DZ_S2P P N PARAMS: VZ=2 VF=0.7
R1 N N1 {VZ/10000} 
E1 N1 P VALUE {MAX(MIN(V(N,P),{VZ}),{-VF})} 
*D1 P N DM1
*.MODEL DM1 D IS=1E-015 TT=1E-010 RS=1m N=0.001 BV={VZ}
.ENDS DZ_S2P
*$
.SUBCKT VCSS_S2P INP INN OUTP OUTN PARAMS: ROFF= 100MEG RON=10m VTH=0.5 VHYS=50m 
S1 OUTP OUTN INP INN SM2
RD INP INN 1T
.MODEL SM2 VSWITCH Roff={ROFF} Ron={RON} VH={VHYS/2} VT={VTH}
.ENDS VCSS_S2P
*$
.SUBCKT CCSS_S2P INP INN OUTP OUTN PARAMS: ROFF= 100MEG RON=10m ITH=0.5 IHYS=50m 
V1 INP INN 0
H1 INP1 INN V1 1
S1 OUTP OUTN INP1 INN SM2
RD1 INP1 INN 1T
.MODEL SM2 VSWITCH Roff={ROFF} Ron={RON} VH={IHYS/2} VT={ITH}
.ENDS CCSS_S2P
*$
.SUBCKT VCTS_S2P INP INN OUTP OUTN PARAMS: ROFF= 100MEG RSAT=10m VTH=0.5 GAIN=10 VSAT=1
S1 OUTP OUTN1 INP INN SM2
RD1 INP INN 1T
.MODEL SM2 VSWITCH Roff={ROFF*1000} Ron={RSAT} VH=0 VT={VTH}
G1 OUTN1 OUTN VALUE {LIMIT((V(INP,INN)-{VTH})*{GAIN},0,100000)}
R2 OUTP OUTN {ROFF}
D1 VSAT1 OUTP DSAT1
.MODEL DSAT1 D IS=1E-015 TT=1E-010 RS=1u N=0.001
E2 VSAT1 OUTN VALUE {VSAT}
.ENDS VCTS_S2P
*$
.SUBCKT CCTS_S2P INP INN OUTP OUTN PARAMS: ROFF= 100MEG RSAT=10m ITH=0.5 GAIN=10 VSAT=1
V1 INP INN 0
H1 INP1 INN V1 1
S1 OUTP OUTN1 INP1 INN SM2
RD1 INP1 INN 1T
.MODEL SM2 VSWITCH Roff={ROFF*1000} Ron={RSAT} VH=0 VT={ITH}
G1 OUTN1 OUTN VALUE {LIMIT((V(INP1,INN)-{ITH})*{GAIN},0,100000)}
R2 OUTP OUTN {ROFF}
D1 VSAT1 OUTP DSAT1
.MODEL DSAT1 D IS=1E-015 TT=1E-010 RS=1u N=0.001
E2 VSAT1 OUTN VALUE {VSAT}
.ENDS CCTS_S2P
*$
.SUBCKT OPTO_S2P A K C E PARAMS: VF=1 RD=100 FPOLE=10k CTR=1 VSAT=1 RON=1 COUT=200p 
V1 A A1 0 
H1 INT1 K V1 1
R1 A1 A2 {RD} 
E1 A2 K VALUE {MIN(V(A,K),{VF})}
RP1 INT1 INT2 {1/(2*3.1416*FPOLE*1E-9)}
CP1 INT2 K 1n
RON C INT2 {RON}
G1 INT2 E VALUE {V(INT1,K)*CTR} 
D1 VSAT1 C DSAT1
.MODEL DSAT1 D IS=1E-015 TT=1E-010 RS=1u N=0.001
E2 VSAT1 E VALUE {VSAT}
COUT C E {COUT}
.ENDS OPTO_S2P
*$
.SUBCKT ASYMDLY_S2P A R Y PARAMS: VH=1 VL=0 VTH=0.5 TDR=50n TDF=50n 
ER1 AINTR R VALUE {IF(V(A,R)>{VTH},1,0)}
RR1 AINTR AINTR2 1 
CR1 AINTR2 R {1.4427*TDR}
EF1 AINTF R VALUE {IF((V(A,R)>{VTH})&(V(AINTR2,R)>0.5),1,0)}
RF1 AINTF AINTF2 1 
CF1 AINTF2 R {1.4427*TDF}
E2 YINT R VALUE {IF((V(AINTF,R)>0.5)|(V(AINTF2,R)>0.5),{VH},{VL})}
R2 YINT Y 1 
C2 Y R 1n
.ENDS ASYMDLY_S2P
*$
.SUBCKT VPWL_S2P P N PARAMS: VH=1 VL=0 VTH=0.5 TDR=50n TDF=50n 