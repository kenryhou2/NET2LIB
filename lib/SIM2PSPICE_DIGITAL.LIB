*$
********************************************************************************************
* (C) Copyright 2021 Texas Instruments Incorporated. All rights reserved.
* TI Confidential - Selective Disclosure
********************************************************************************************
*SIM2PSPICE Library 
*Last update :Jan 7th 2022
*Contact : eric.lee@ti.com, david.baba@ti.com
********************************************************************************************
*$
.SUBCKT XOR2_S2P A B Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ (V(A,R)<={VTH}&
+ V(B,R)<={VTH}) | 
+ (V(A,R)>{VTH}&
+ V(B,R)>{VTH}) 
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS XOR2_S2P
*$
.SUBCKT OR2_S2P A B Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS OR2_S2P
*$
.SUBCKT OR2A_S2P A B Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS OR2A_S2P
*$
.SUBCKT OR3_S2P A B C Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS OR3_S2P
*$
.SUBCKT OR3A_S2P A B C Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS OR3A_S2P
*$
.SUBCKT OR4_S2P A B C D Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}|
+ V(D,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS OR4_S2P
*$
.SUBCKT OR4A_S2P A B C D Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}|
+ V(D,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}|
+ V(D,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS OR4A_S2P
*$
.SUBCKT NOR2_S2P A B Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NOR2_S2P
*$
.SUBCKT NOR3_S2P A B C Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NOR3_S2P
*$
.SUBCKT NOR4_S2P A B C D Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}|
+ V(B,R)>{VTH}|
+ V(C,R)>{VTH}|
+ V(D,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NOR4_S2P
*$
.SUBCKT AND2_S2P A B Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS AND2_S2P
*$
.SUBCKT AND2A_S2P A B Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS AND2A_S2P
*$
.SUBCKT AND3_S2P A B C Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS AND3_S2P
*$
.SUBCKT AND3A_S2P A B C Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS AND3A_S2P
*$
.SUBCKT AND4_S2P A B C D Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}&
+ V(D,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS AND4_S2P
*$
.SUBCKT AND4A_S2P A B C D Y NY R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}&
+ V(D,R)>{VTH}
+ ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}&
+ V(D,R)>{VTH}
+ ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS AND4A_S2P
*$
.SUBCKT NAND2_S2P A B Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NAND2_S2P
*$
.SUBCKT NAND3_S2P A B C Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1nv
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NAND3_S2P
*$
.SUBCKT NAND4_S2P A B C D Y R PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(
+ V(A,R)>{VTH}&
+ V(B,R)>{VTH}&
+ V(C,R)>{VTH}&
+ V(D,R)>{VTH}
+ ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS NAND4_S2P
*$
.SUBCKT COMP_S2P INP INN OUT REF PARAMS: VH=1 VL=0 VHYS=50m RC=1n
E1 INP1 INP VALUE {IF(V(INP1)>V(INN),{VHYS}/2,-{VHYS}/2)}
R1 INP1 INP 10T
E2 OUTINT REF VALUE {IF(V(INP1)>V(INN),{VH},{VL})}
RF OUTINT OUT 1
CF OUT REF {RC}
.ENDS COMP_S2P
*$
.SUBCKT COMPA_S2P INP INN OUT NOUT REF PARAMS: VH=1 VL=0 VHYS=50m RC=1n
E1 INP1 INP VALUE {IF(V(INP1)>V(INN),{VHYS}/2,-{VHYS}/2)}
R1 INP1 INP 10T
E2 OUTINT REF VALUE {IF(V(INP1)>V(INN),{VH},{VL})}
RF OUTINT OUT 1
CF OUT REF {RC}
E3 NOUTINT REF VALUE {IF(V(INP1)>V(INN),{VL},{VH})}
RF2 NOUTINT NOUT 1
CF2 NOUT REF {RC}
.ENDS COMPA_S2P
*$
.SUBCKT INV_S2P A R Y PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(V(A,R) > {VTH} ,{VL},{VH})}
RF YINT Y 1
CF Y R {RC}
.ENDS INV_S2P
*$
.SUBCKT BUF_S2P A R Y PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(V(A,R) > {VTH} ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
.ENDS BUF_S2P
*$
.SUBCKT BUFA_S2P A R Y NY PARAMS: VH=1 VL=0 VTH=0.5 RC=1n
E1 YINT R VALUE {IF(V(A,R) > {VTH} ,{VH},{VL})}
RF YINT Y 1
CF Y R {RC}
E2 NYINT R VALUE {IF(V(A,R) > {VTH} ,{VL},{VH})}
RF2 NYINT NY 1
CF2 NY R {RC}
.ENDS BUFA_S2P
*$
.SUBCKT SRFFSD_S2P S R Q QN RTN PARAMS: VH=1 VL=0 VTH=0.5 RC=1n IC=0 
ES S1 RTN VALUE {IF(V(S,RTN)>{VTH}, {VH}, {VL})}

E1 INT1 RTN VALUE {IF(V(S1,RTN)<={VTH} & V(R,RTN)<={VTH},V(Q,RTN),V(S1,RTN))}

EQ Q1 RTN VALUE {IF(V(INT1,RTN)<={VTH}, {VL}, {VH})}
EQN QN1 RTN VALUE {IF(V(Q1,RTN)>{VTH}, {VL}, {VH})}
RF1 Q1 Q 1
CF1 Q RTN {RC} IC={IC}
RF2 QN1 QN 1 
CF2 QN RTN {RC} IC={VH-IC}
.ENDS SRFFSD_S2P
*$
.SUBCKT SRFFRD_S2P S R Q QN RTN PARAMS: VH=1 VL=0 VTH=0.5 RC=1n IC=0
ER R1 RTN VALUE {IF(V(R,RTN)>{VTH}, {VH}, {VL})} 

E1 INT1 RTN VALUE {IF(V(S,RTN)<={VTH} & V(R1,RTN)<={VTH},V(QN,RTN),V(R1,RTN))}

EQN QN1 RTN VALUE {IF(V(INT1,RTN)<={VTH}, {VL}, {VH})}
EQ Q1 RTN VALUE {IF(V(QN1,RTN)>{VTH}, {VL}, {VH})}
RF1 QN1 QN 1
CF1 QN RTN {RC} IC={IC}
RF2 Q1 Q 1 
CF2 Q RTN {RC} IC={VH-IC}
.ENDS SRFFRD_S2P
*$
.SUBCKT DFF_S2P D CLK Q QN RTN PARAMS: VH=1 VL=0 VTH=0.5 RC=1n IC=0 
ECLK CLK1 RTN VALUE {IF(V(CLK,RTN)>{VTH},{VH},{VL})}
RCLK CLK1 CLK2 1
CCLK CLK2 RTN 5n
EEDGE CLK3 RTN VALUE {IF(V(CLK1,RTN)>{VTH} & V(CLK2,RTN)<={VTH},{VH},{VL})}
ED D1 RTN VALUE {IF(V(D,RTN)>{VTH},{VH},{VL})}   

E1 INT1 RTN VALUE {IF(V(CLK3,RTN)>{VTH},V(D1,RTN),V(Q1,RTN))}

EQ Q1 RTN VALUE {IF(V(INT1, RTN)>{VTH}, {VH}, {VL})}
EQN QN1 RTN VALUE {IF(V(Q1, RTN)>{VTH}, {VL}, {VH})}
RF1 Q1 Q 1
CF1 Q RTN {RC} IC={IC}
RF2 QN1 QN 1 
CF2 QN RTN {RC} IC={VH-IC}
.ENDS DFF_S2P 
*$
.SUBCKT DFFPC_S2P D CLK PRE CLR Q QN RTN PARAMS: VH=1 VL=0 VTH=0.5 RC=1n IC=0 
ECLK CLK1 RTN VALUE {IF(V(CLK,RTN)>{VTH},{VH},{VL})}
RCLK CLK1 CLK2 1
CCLK CLK2 RTN 5n
EEDGE CLK3 RTN VALUE {IF(V(CLK1,RTN)>{VTH} & V(CLK2,RTN)<={VTH},{VH},{VL})}
ED D1 RTN VALUE {IF(V(D,RTN)>{VTH},{VH},{VL})}   

E1 INT1 RTN VALUE {IF(V(CLK3,RTN)>{VTH},V(D1,RTN),V(Q1,RTN))}
E2 INT2 RTN VALUE {IF(V(CLR,RTN)>{VTH},{VL},IF(V(PRE,RTN)>{VTH},{VH},V(INT1,RTN)))}

EQ Q1 RTN VALUE {IF(V(INT2, RTN)>{VTH}, {VH}, {VL})}
EQN QN1 RTN VALUE {IF(V(Q1, RTN)>{VTH}, {VL}, {VH})}
RF1 Q1 Q 1
CF1 Q RTN {RC} IC={IC}
RF2 QN1 QN 1 
CF2 QN RTN {RC} IC={VH-IC}
.ENDS DFFPC_S2P 
*$
.SUBCKT ONESHOT_S2P A R Y PARAMS: VH=1 VL=0 VTH=0.5 PW=100n 
E1 INTA 0 VALUE {IF(V(A,R)>{VTH}|V(Y,R)>{VTH},1,0)}
R1 INTA INTA1 1 
C1 INTA1 0 {1.4427*PW}  
E2 INTB 0 VALUE {IF(V(A,R)>{VTH}|V(Y,R)>{VTH},0,1)}
R2 INTB INTB1 1 
C2 INTB1 0 1n
E3 YINT R VALUE {IF(V(INTA,R)>0.5 & V(INTA1)<0.5,1,0)}
R3 YINT Y 1 
C3 Y R 1n
S1 INTA1 0 INTB1 0 SM1
.MODEL SM1 VSWITCH ROFF=1T RON=1 VOFF=0.25 VON=0.75
.ENDS ONESHOT_S2P
*$
